apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.github.triplet.play'

def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(file("../../signing/keystore.properties")))
} catch (Exception ignore) {}

android {
    compileSdkVersion build_versions.target_sdk

    defaultConfig {
        applicationId "com.satis.app"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 48
        versionName "48.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'] ?: "dummy")
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

play {
    defaultToAppBundles = true
    serviceAccountCredentials = file("../../signing/service-account.json")
    track = 'internal'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Firebase
    implementation deps.firebase_core
    implementation deps.firebase_firestore

    // RxJava
    implementation deps.rx_android
    implementation deps.rxjava2

    // Dagger
    implementation deps.dagger.runtime
    implementation deps.dagger.android
    implementation deps.dagger.android_support
    kapt deps.dagger.compiler
    kapt deps.dagger.android_support_compiler

    // Kotlin
    implementation deps.kotlin.stdlib

    // Android Ktx
    implementation deps.android_ktx

    // Tiny Dancer
    implementation deps.tinydancer

    // Conductor
    implementation deps.conductor

    // Architecture Components
    implementation deps.lifecycle.runtime
    implementation deps.lifecycle.extensions
    kapt deps.lifecycle.compiler
    implementation deps.room.runtime
    kapt deps.room.compiler
    implementation deps.room.rxjava2
    implementation deps.work_manager.runtime

    // Support libraries
    implementation deps.support.app_compat
    implementation deps.support.v4
    implementation deps.support.design
    implementation deps.support.constraint_layout
    implementation deps.support.recyclerview
    implementation deps.support.cardview

    // Unit Testing
    testImplementation deps.junit

    // Android Testing
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.room.testing
    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation deps.espresso.intents
}
