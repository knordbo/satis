apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.github.triplet.play'
apply plugin: "com.github.ben-manes.versions"

def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(file("../keystore.properties")))
} catch (Exception ignore) {
}

android {
    compileSdkVersion buildVersions.targetSdk

    defaultConfig {
        applicationId "com.satis.app"
        minSdkVersion buildVersions.minSdk
        targetSdkVersion buildVersions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'] ?: "dummy")
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            buildConfigField "Long", "BUILD_TIME", "0L"
        }
        release {
            buildConfigField "Long", "BUILD_TIME", System.currentTimeMillis().toString() + "L"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

play {
    defaultToAppBundles = true
    serviceAccountCredentials = file("../service-account.json")
    track = 'internal'
    resolutionStrategy = 'auto'
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX
    implementation deps.androidx.appCompat
    implementation deps.androidx.cardView
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.coreKtx
    implementation deps.androidx.lifecycle.extensions
    implementation deps.androidx.lifecycle.runtime
    implementation deps.androidx.recyclerView
    implementation deps.androidx.room.runtime
    implementation deps.androidx.room.rxJava2
    kapt deps.androidx.lifecycle.compiler
    kapt deps.androidx.room.compiler

    // Firebase
    implementation deps.firebase.core
    implementation deps.firebase.firestore

    // Koin
    implementation deps.koin.android
    implementation deps.koin.androidViewmodel

    // Kotlin
    implementation deps.kotlin.stdlib

    // Material Design
    implementation deps.material

    // MvRx
    implementation deps.mvrx

    // RxJava
    implementation deps.rxAndroid
    implementation deps.rxJava2

    // Tiny Dancer
    implementation deps.tinydancer

    // Work Manager
    implementation(deps.workManager.runtime) {
        // TODO remove after work manager alpha
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    // Unit Testing
    testImplementation deps.junit

    // Android Testing
    androidTestImplementation deps.androidx.test.runner
    androidTestImplementation deps.androidx.test.rules
    androidTestImplementation deps.androidx.room.testing
    androidTestImplementation deps.androidx.coreTesting
    androidTestImplementation deps.androidx.espresso.core
    androidTestImplementation deps.androidx.espresso.contrib
    androidTestImplementation deps.androidx.espresso.intents
}
